plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

group = 'io.github.maowimpl'
archivesBaseName = 'owo'
version = '2.0.0'

compileJava.options.encoding = 'UTF-8'

javadoc {
    options.encoding = 'UTF-8'
}

java {
    withJavadocJar()
    withSourcesJar()
}

def isReleaseVersion = !version.endsWith('SNAPSHOT')

publishing {
    repositories {
        maven {
            url = isReleaseVersion
                    ? 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
                    : 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
            credentials {
                username = project.hasProperty('nexusUsername') ? nexusUsername : "UNKNOWN"
                password = project.hasProperty('nexusPassword') ? nexusPassword : "UNKNOWN"
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            pom {
                groupId = 'io.github.maowimpl'
                name = 'owo'
                description = 'Advanced Java OwOifier'
                url = 'https://github.com/MaowImpl/owo/'

                from components.java

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }

                scm {
                    connection = 'scm:git:git@github.com:maowimpl/owo.git'
                    developerConnection = 'scm:git:git@github.com:maowimpl/owo.git'
                    url = 'https://github.com/MaowImpl/owo/'
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

tasks.withType(Sign) {
    onlyIf { isReleaseVersion }
}